.# This file generates the tables on the aya wiki

export ::op_page_str

import ::dataframe

{.lines :# {"  "\+} "\n" *}:indent;

.# Type Key
{,

    [
        ["Number" 'N]
        ["String" 'S]
        ["Char" 'C]
        ["Block" 'B]
        ["Dict" 'D]
        ["Symbol" 'J]
    ]:data;
  
    ["Type" "Abbreviation"]:colnames;
} dataframe! :abbr_table;

{, 1:noindex } abbr_table.to_csv indent :abbr_table_csv;


{:C "\"" "" .&}:sym_to_str;
{:& "" =! {"``"\+ "``" +} ?}:code;
.# "__foo__" => "\_\_foo\_\_"
{#{x, x'_=! x "\_" .?}S}:escape_underscore;

{,
    0M?:#{op, [
        op.name code
        op.ops :K #sym_to_str '|* code
        op.ops :# {k v,
            k sym_to_str code :&""={"n/a"+}? " : $v" +
        }:V ", " *
        op.overload "/" | #code " / " *
    ]}:data;

    ["Name" "Args" "Ops" "Overload"]:colnames;
}dataframe! :op_table;

{, 1:noindex 1:border } op_table.to_csv indent :op_table_csv;

op_table_csv
("\\\\" "\\\\\\\\" .&)
("`" "\`" .&)
.# ("_" "\\\\_" .&)
:op_table_csv;

"""Operators
=========

Type Abbreviations
------------------

.. csv-table::

$abbr_table_csv

Operator Table
--------------

.. csv-table::

$op_table_csv
""" :% :op_page_str;

